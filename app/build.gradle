apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'hugo'
//apply plugin: 'loglifecycle'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jdepend'
apply plugin: 'findbugs'



/*
  Project Module metadata, some Project objects are read only but we need to set them for
  a human friendly name/title for our reports so we do this as than we get around the
  default AndroidStudio thing of naming our first app module app.

  For example, even though through Reports displayName are read-only but we can through
  changing xsl and passing vars via ant builder get the header of the report to change to
  reflect our project.ext stuff defined below.
 */
ext {



    //a little groovy syntax sugar and some gradle knowledge gives a nice
    //human style project name rather than default IDE/gradle app name
    ourProjectName = '${rootProject.projectDir.name}'+ '${project.name.capitalize()}'

    ourProjectLead = 'Fred Grott'
    ourProjectDescription = 'basic app showing build script with app source code'
    //javadocFooter = "Copyright 2014-${rootProject.ext.reportBuildDate} by $project.ext.ourProjectLead Apache License 2.0"

    archiveVariant = ['debug','release']
    javadocVariant = ['release']

    jdependMainXmlOutput = 'build/reports/jdepend/jdepend-main.xml'
    jdependMainHtmlOutput = 'build/reports/jdepend/jdepend-main.html'


    checkstyleMainXmlOutput = 'build/reports/checkstyle/checkstyle-main.xml'
    checkstyleMainHtmlOutput = 'build/reports/checkstyle/checkstyle-main.html'
    checkstyleAndroidTestXmlOutput = 'build/reports/checkstyle/checkstyle-androidtest.xml'
    checkstyleAndroidTestHtmlOutput = 'build/reports/checkstyle/checkstyle-androidtest.html'

    pmdMainXmlOutput = 'build/reports/pmd/pmd-main.xml'
    pmdMainHtmlOutput = 'build/reports/pmd/pmd-main.html'
    pmdAndroidTestXmlOutput = 'build/reports/pmd/pmd-androidtest.xml'
    pmdAndroidTestHtmlOutput = 'build/reports/pmd/pmd-androidtest.html'


    findbugsMainXmlOutput = 'build/reports/findbugs/findbugs-main.xml'
    findbugsMainHtmlOuput = 'build/reports/findbugs/findbugs-main.html'

    javancssMainXmlOutput = 'build/reports/javancss/javancss-main.xml'
    javancssMainHtmlOutput = 'build/reports/javancss/javancss-main.html'
    javancssAndroidTestXmlOutput = 'build/reports/javancss/javancss-androidTest.xml'
    javancssAndroidTestHtmlOutput = 'build/reports/javancss/javancss-androidTest.html'

    classycleMainXmlOutput = 'build/reports/classycle/classycle-main.xml'
    classycleMainHtmlOutput = 'build/reports/classycle/classycle-main.html'


    classycleXslFile = '../config/classycle/reportXMLtoHTML.xsl'
    jdependXslFile = '../config/jdepend/jdepend.xsl'
    checkstyleXslFile = '../config/checkstyle/checkstyle-noframes-severity-sorted.xsl'
    pmdXslFile = '../config/pmd/pmd-nicerhtml.xsl'
    javancssXslFile = '../config/javancss/javancss2html.xsl'
    findbugsXslFile = '../config/findbugs/findbugs-html.xsl'

    checkstyleChecksFile = '../config/checkstyle/android_checkstyle_5_7_checks.xml'
    findbugsExcludeFilterFile = '../config/findbugs/findbugs-filter.xml'


}



android {
    compileSdkVersion rootProject.ext.ourCompileSdkVersion
    buildToolsVersion rootProject.ext.ourBuildToolsVersion



    defaultConfig {
        applicationId "com.grottworkshop.fgbuildtutorialandroid"
        minSdkVersion rootProject.ext.ourMinSdkVersion
        targetSdkVersion rootProject.ext.ourTargetSdkVersion
        versionCode rootProject.ext.ourVersionCode
        versionName "${rootProject.ext.appVersionMajor}.${rootProject.ext.appVersionMinor}.${rootProject.ext.appVersionPatch}"

        buildConfigField "String", "BUILD_TIME", "\"${rootProject.ext.buildTime}\""

        testApplicationId 'com.grottworkshop.fgbuildtutorialandroid.test'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testHandleProfiling true
        testFunctionalTest true
    }
    /*
    dexOptions already handled and set in subprojects block of root
    build.gradle file
     */


    testOptions {
        resultsDir = rootProject.ext.testOptionsResultsDir

    }

    aaptOptions {
        noCompress 'txt'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    signingConfigs {

        release {
            //props stored in gradle.properties at userhome .gradle subfolder

            storeFile file(FREDGROTT_RELEASE_STORE_FILE)
            storePassword FREDGROTT_RELEASE_STORE_PASSWORD
            keyAlias FREDGROTT_RELEASE_KEY_ALIAS
            keyPassword FREDGROTT_RELEASE_KEY_PASSWORD
        }

    }

    /*
    Enables diamond operator, multi-catch, strings in switches for our
    minSdkVersion to compileSdkVersion range.
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /*
    The android gradle plugin loads a duplicate jacoco plugin thus for
    instrumented unit testing we use the internal duplicate jacoco plugin
    from the android gradle plugin hence only needing the configuration closure.

    Jacoco reports can be generated by rooted emulator with the command:
         $ gradlew createDebugCoverageReport
    Or on rooted connected devices with the command:
         $ gradlew connectedCheck
    Coverage reports will be found in
          build/[reports/coverage
     */
    jacoco {
        version = '0.7.1.20140508213'


    }




    /*
    In normal android mobile dev shops you might have stakeholders that
    you need to show app demos to. I use debug for that purpose, however you can
    init a client build type using debug for that purpose. Remember, it is helpful
    if you rename the apk with appName, build variant, versionCode in the file name and
    of course archive a copy in a zip file with the same file naming scheme.
     */
    buildTypes {

        /*
        We define some buildConfigField defs here to use
         */
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def LOG_HTTP_REQUESTS = "LOG_HTTP_REQUESTS"
        def REPORT_CRASHES = "REPORT_CRASHES"
        def ENABLE_VIEW_SERVER = "ENABLE_VIEW_SERVER"
        def ENABLE_SHARING = "ENABLE_SHARING"
        def DEBUG_IMAGES = "DEBUG_IMAGES"


        debug {
            debuggable true
            minifyEnabled false
            // dagger jacaco conflict issue is resolved, dx change after buildtoolsversion 21
            testCoverageEnabled true

            // here we go, versionNameSuffix with build date!
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev-' + rootProject.ext.buildTime

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, TRUE
            buildConfigField BOOLEAN, ENABLE_SHARING, TRUE
            buildConfigField BOOLEAN, DEBUG_IMAGES, TRUE


        }



        release {
            minifyEnabled false

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE



            /*
            Separate out your proguard rules into files for different areas/groups as it makes it easier to
            update the configurations.
             */
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro' , 'proguard-googleplayservices.txt', 'proguard-butterknife.txt','proguard-dagger1.txt','proguard.project.txt'

            applicationIdSuffix '.release'
            versionNameSuffix '-release-' + rootProject.ext.buildTime
            signingConfig signingConfigs.release

        }
    }

    /*
    Lint is already setup as a plugin to iterate over all build variants.
     */
    lintOptions {
        lintConfig file("my-custom-lint.xml")

        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // turn off checking the given issue id's
        //disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        //enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true

        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport true
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("build/reports/lint/lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("build/reports/lint/lint-report.html")

        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // Set the severity of the given issues to fatal (which means they will be
        // checked during release builds (even if the lint target is not included)
        fatal 'NewApi', 'InlineApi'
        // Set the severity of the given issues to error
        error 'Wakelock', 'TextViewEdits'
        // Set the severity of the given issues to warning
        warning 'ResourceAsColor'
        // Set the severity of the given issues to ignore (same as disabling the check)
        ignore 'TypographyQuotes'


    }

}



/*
Reports get generated and placed in build/spoon/${Test_VARIANT} folder.
To instead of running on all connected devices to execute on a specific
set of devices uncomment devices line and replace serial number with the
serial numbers of devices you want to target.
 */
spoon {
    // devices = ['333236E9AE5800EC']
    debug = true
    //className = 'fully.qualified.TestCase'

    //methodName = 'testMyApp'
}





configurations{
    checkstyle
    pmd
    jdepend
    androidJacocoAgent
    androidJacocoAnt
    javancss
    findbugs

}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile rootProject.ext.androidsupportapptcompatv7
    compile rootProject.ext.androidsupportannotations


    checkstyle rootProject.ext.codeqacheckstyle
    pmd rootProject.ext.codeqapmd
    jdepend rootProject.ext.codeqajdepend
    jdepend rootProject.ext.codeqajdependant
    androidJacocoAgent rootProject.ext.codeqajacoco
    androidJacocoAnt rootProject.ext.codeqajacocoant
    findbugs rootProject.ext.codeqafindbugs
    javancss rootProject.ext.codeqajavancss



    compile rootProject.ext.retrofit

    compile rootProject.ext.dagger
    compile rootProject.ext.butterknife

    compile rootProject.ext.otto
    compile rootProject.ext.rxjava
    compile rootProject.ext.timber

    compile rootProject.ext.okhttp
    compile rootProject.ext.okio
    compile rootProject.ext.okhttpurlconnection

    debugCompile rootProject.ext.retrofitmock

    provided rootProject.ext.daggercompiler

    androidTestCompile rootProject.ext.espresso

    androidTestCompile rootProject.ext.espressocontrib



    androidTestCompile rootProject.ext.assertjandroid
    androidTestCompile rootProject.ext.assertjandroidsupportv4



    androidTestCompile rootProject.ext.mockito
    androidTestCompile rootProject.ext.dexmaker
    androidTestCompile(rootProject.ext.dexmakermockito) {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
        exclude module: 'mockito-core'

    }

    androidTestCompile rootProject.ext.spoonclient

}

/*
Codeqa and Javadocs accounting for sourceSets and productFlavors is pain-in-the-ass
for android gradle plugin versions 1.x.

Codeqa:
         project.sourceSets still has things empty due to the switch from a
         gradle internal class to the new gradle sourceSet model that involves native.
         Thus, we hack/cheat in that we specify src as soruce with include/exclude so
         that all sourceSets that have source get checked. Findbugs and Jdepend
         get constrained to main SourceSet only.

Javadoc:
         We use an array to manually constrain javadoc task generation to only those
         variants that have sources.
 */

android.applicationVariants.all { variant ->

    //rename apk file  block
    // modified from:
    // https://code.google.com/p/android/issues/detail?id=79123
    // per Nick's(VokalInteractive) example
    // Nick's code did not account for productFlavors[0] being null
    // and the other use cases..mine covers all use cases
    if (variant.productFlavors[0] == null){
        //we need to wrap this to check for is output null and
        //to check that we have an .apk file to rename
        //as we do not rename library aar's
        def oldFile = variant.outputs[0].outputFile
        if (oldFile !=null){
            if (oldFile.name.endsWith('.apk')){
                variant.outputs[0].outputFile = new File(variant.outputs[0].outputFile.parent,
                        project.ext.ourProjectName + "_"
                                + variant.buildType.name + "_"
                                + android.defaultConfig.versionCode + "_"
                                + android.defaultConfig.versionName + ".apk")
            }
        }

    }else{
        def oldFile = variant.outputs[0].outputFile
        if (oldFile != null){
            if (oldFile.name.endsWith('.apk')){
                variant.outputs[0].outputFile = new File(variant.outputs[0].outputFile.parent,
                        project.ext.ourProjectName + "_"
                                + variant.productFlavors[0].name + "_"
                                + variant.buildType.name + "_"
                                + variant.mergedFlavor.versionCode + "_"
                                + variant.mergedFlavor.versionName + ".apk")
            }
        }

    }

    // copy apk block
    if (variant.name in archiveVariant) {
        def taskSuffix = variant.buildType.name.capitalize()
        def version = "${android.defaultConfig.versionCode}(${android.defaultConfig.versionName})"
        def destination = "${rootProject.projectDir}/archive/apks/${project.ext.ourProjectName}/${variant.name}/${version}"
        def assembleTaskName = "assemble${taskSuffix}"
        if (tasks.findByName(assembleTaskName)) {
            def copyAPKTask = tasks.create(name: "archive${taskSuffix}", type: Copy) {
                description "Archive/copy apk and proguard mappings.txt to a versioned folder."
                from("${buildDir}") {
                    include "**/proguard/${variant.buildType.name}/mapping.txt"
                    include "**/apk/${variant.outputs[0].outputFile.name}"
                }
                into destination
                eachFile { file ->
                    file.path = file.name // to get flat copy

                }
                includeEmptyDirs = false

            }
            tasks[assembleTaskName].finalizedBy = [copyAPKTask]
        }

    }

    //javadoc
    if (variant.name in javadocVariant){
        // dynamic javadoc task setup, no automatic hooks
        // by variant as we grab our sourceSet via variants
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description = 'Javadoc of $variant.name'
            group = 'documentaion'
            // When JavaBase plugin loads it only sets two javadocs, title and destinationDir
            // thus if we need different we need to set it ourselves
            title = "$project.ext.ourProjectName $android.defaultConfig.versionCode $android.defaultConfig.versionName API"
            // I do not think JavaBase plugin sets docsDir as I was getting symbol not resolved
            destinationDir = new File("${rootProject.ext.buildDocsBaseDir}/${variant.name}/javadoc")
            source = variant.javaCompile.source
            def myAndroidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            def myGooglePlayServicesJar = "${android.sdkDirectory}/extras/google/google_play_services/libproject/google-play-services_lib/libs/google-play-services.jar"
            classpath = files(variant.javaCompile.classpath.files, myAndroidJar, myGooglePlayServicesJar)
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            options {
                docTitle = "Generated Javadoc for $project.ext.ourProjectName for $variant.name"
                windowTitle = "javadoc:$variant.name"
                memberLevel = JavadocMemberLevel.PROTECTED
                author = true
                header = project.ext.ourProjectName
                footer = "Copyright 2014-$rootProject.ext.reportBuildDate by $project.ext.ourProjectLead Apache License 2.0"
                linksOffline('http://d.android.com/reference', '${android.sdkdirectory}/docs/reference')
                stylesheetFile = new File("${rootProject.ext.javadocsJDK7Stylesheet}")

            }
            doLast {
                copy {

                    from "${project.buildDir}/docs/${variant.name}/javadoc"
                    into "${rootProject.projectDir}/archive/docs/${variant.name}/${android.defaultConfig.versionCode}/javadoc"
                }
            }

        }

    }






}


task jdependDebugMain{
    description = 'completes jdepend analysis of main sourceset'
    group = 'verification'

    ant.taskdef(name:'jdependreport', classname:'org.apache.tools.ant.taskdefs.optional.jdepend.JDependTask', classpath: configurations.jdepend.asPath)
    ant.jdependreport(outputfile: jdependMainXmlOutput, format: 'xml'){
        exclude(name:'**/R.*')
        exclude(name:'android.*')
        classespath{
            pathElement(location: 'build/intermediates/classes/debug')
        }
    }
    ant.xslt(in:jdependMainXmlOutput, style:jdependXslFile, out:jdependMainHtmlOutput)
}
check.dependsOn("jdependDebugMain")

task findbugsMain(type:FindBugs){
    description = 'completes findbugs analysis of debug/main'
    group = 'verification'


    ignoreFailures = true
    classes = fileTree('build/intermediates/classes/debug')
    source = fileTree('src/main/java')
    classpath = files()
    effort = 'max'

    excludeFilter = file(findbugsExcludeFilterFile)

    reports {
        xml.enabled = true
        xml.destination = findbugsMainXmlOutput
    }

    doLast{
        ant.xslt(in:findbugsMainXmlOutput, style:findbugsXslFile, out:findbugsMainHtmlOuput)
    }

}
check.dependsOn("findbugsMain")



task checkstyleMain(type:Checkstyle) {

    description = 'performs checkstyle checks on main sourceSet'
    group = 'verififcation'

    //we can use annotation of SuppressWarning('checkstyle:rule') to
    //ignore certain violations hence this setting to false
    ignoreFailures = false
    showViolations = true

    //we want the main sourceSet only
    source = 'src/main/java'
    include '**/*.java'

    // empty classpath
    classpath = files()

    configFile = file(checkstyleChecksFile)

    reports{
        xml.enabled = true
        xml.destination = checkstyleMainXmlOutput
    }
    doLast{

        ant.xslt(in:checkstyleMainXmlOutput, style:checkstyleXslFile, out:checkstyleMainHtmlOutput)
    }

}
check.dependsOn("checkstyleMain")

task checkstyleAndroidTest(type:Checkstyle){
    description = 'performs checkstyle checks on the androidTest sourceSet'
    group = 'verification'
    //we can use annotation of SuppressWarning('checkstyle:rule') to
    //ignore certain violations hence this setting to false
    ignoreFailures = false
    showViolations = true

    //we want the androidTest sourceSet only
    source = 'src/androidTest/java'
    include '**/*.java'

    // empty classpath
    classpath = files()

    configFile = file(checkstyleChecksFile)

    reports{
        xml.enabled = true
        xml.destination = checkstyleAndroidTestXmlOutput
    }
    doLast{
        ant.xslt(in:checkstyleAndroidTestXmlOutput, style:checkstyleXslFile, out:checkstyleAndroidTestHtmlOutput)
    }
}
check.dependsOn("checkstyleAndroidTest")

task pmdMain(type:Pmd) {
    description = 'performs pmd checks on main sourceSet'
    group = 'verification'
    //we assume that NOPMD comment might not be enabled through gradle ant builder for this task
    ignoreFailures = true
    consoleOutput = true

    ruleSets = ["java-basic", "java-braces", "java-strings", "java-android", "java-junit"]

    source = 'src/main/java'
    include '**/*.java'

    reports {
        xml.enabled = true
        xml.destination = pmdMainXmlOutput
        html.enabled = false

    }

    doLast{
        ant.xslt(in:pmdMainXmlOutput, style:pmdXslFile, out:pmdMainHtmlOutput)
    }


}
check.dependsOn("pmdMain")

task pmdAndroidTest(type:Pmd){
    description = 'performs pmd checks on the AndroidTest sourceSet'
    group = 'verififcation'

    //we assume that NOPMD comment might not be enabled through gradle ant builder for this task
    ignoreFailures = true
    consoleOutput = true

    ruleSets = ["java-basic", "java-braces", "java-strings", "java-android", "java-junit"]

    source = 'src/androidTest/java'
    include '**/*.java'

    reports {
        xml.enabled = true
        xml.destination = pmdAndroidTestXmlOutput
        html.enabled = false

    }

    doLast{

        ant.xslt(in:pmdAndroidTestXmlOutput, style:pmdXslFile, out:pmdAndroidTestHtmlOutput)
    }


}
check.dependsOn("pmdAndroidTest")











task javancssMain  {
    description = 'completes javancss analysis on the main sourceSet'
    group = 'verification'

    def jsReportsDir = new File("$project.buildDir/reports/javancss")
    jsReportsDir.mkdirs()


    ant.taskdef(name:'javancss', classname:'javancss.JavancssAntTask', classpath:configurations.javancss.asPath)
    ant.javancss(srcdir:'src/main/java', generateReport:true, outputfile: javancssMainXmlOutput, format:'xml')

    ant.xslt(in: javancssMainXmlOutput, style: '../config/javancss/javancss2html.xsl',out: javancssMainHtmlOutput)


}

check.dependsOn('javancssMain')

task javancssAndroidTest {
    description = 'completes javancss analysis of the androidTest sourceSet'
    group = 'verification'

    def jsReportsDir = new File("$project.buildDir/reports/javancss")
    jsReportsDir.mkdirs()


    ant.taskdef(name:'javancss', classname:'javancss.JavancssAntTask', classpath:configurations.javancss.asPath)
    ant.javancss(srcdir:'src/androidTest/java', generateReport:true, outputfile: javancssAndroidTestXmlOutput, format:'xml')

    ant.xslt(in: javancssAndroidTestXmlOutput, style: '../config/javancss/javancss2html.xsl',out: javancssAndroidTestHtmlOutput)


}

check.dependsOn("javancssAndroidTest")
