apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'hugo'
apply plugin: 'loglifecycle'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jdepend'


/*
  Project Module metadata, some Project objects are read only but we need to set them for
  a human friendly name/title for our reports so we do this as than we get around the
  default AndroidStudio thing of naming our first app module app.

  For example, even though through Reports displayName are read-only but we can through
  changing xsl and passing vars via ant builder get the header of the report to change to
  reflect our project.ext stuff defined below.
 */
ext {
    ourProjectName = 'BasicApp'
    ourProjectLead = 'Fred Grott'
    ourProjectDescription = 'basic app showing build script with app source code'
    javadocFooter = "Copyright 2014-$rootProject.ext.reportBuildDate by $project.ext.ourProjectLead Apache License 2.0"
}



android {
    compileSdkVersion rootProject.ext.ourCompileSdkVersion
    buildToolsVersion rootProject.ext.ourBuildToolsVersion



    defaultConfig {
        applicationId "com.grottworkshop.fgbuildtutorialandroid"
        minSdkVersion rootProject.ext.ourMinSdkVersion
        targetSdkVersion rootProject.ext.ourTargetSdkVersion
        versionCode rootProject.ext.ourVersionCode
        versionName "${rootProject.ext.appVersionMajor}.${rootProject.ext.appVersionMinor}.${rootProject.ext.appVersionPatch}"

        buildConfigField "String", "BUILD_TIME", "\"${rootProject.ext.buildTime}\""

        testApplicationId 'com.grottworkshop.fgbuildtutorialandroid.test'
        testInstrumentationRunner 'com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner'
        testHandleProfiling true
        testFunctionalTest true
    }
    /*
    dexOptions already handled and set in subprojects block of root
    build.gradle file
     */

    testOptions {
        resultsDir = rootProject.ext.instrumentedTestsReportsDir

    }

    aaptOptions {
        noCompress 'txt'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    signingConfigs {

        release {
            //props stored in gradle.properties at userhome .gradle subfolder

            storeFile file(FREDGROTT_RELEASE_STORE_FILE)
            storePassword FREDGROTT_RELEASE_STORE_PASSWORD
            keyAlias FREDGROTT_RELEASE_KEY_ALIAS
            keyPassword FREDGROTT_RELEASE_KEY_PASSWORD
        }

    }

    /*
    Enables diamond operator, multi-catch, strings in switches for our
    minSdkVersion to compileSdkVersion range.
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /*
    The android gradle plugin loads a duplicate jacoco plugin thus for
    instrumented unit testing we use the internal duplicate jacoco plugin
    from the android gradle plugin hence only needing the configuration closure.

    Jacoco reports can be generated by rooted emulator with the command:
         $ gradlew createDebugCoverageReport
    Or on rooted connected devices with the command:
         $ gradlew connectedCheck
    Coverage reports will be found in
          build/[reports/coverage
     */
    jacoco {
        version = '0.7.1.20140508213'


    }




    /*
    In normal android mobile dev shops you might have stakeholders that
    you need to show app demos to. I use debug for that purpose, however you can
    init a client build type using debug for that purpose. Remember, it is helpful
    if you rename the apk with appName, build variant, versionCode in the file name and
    of course archive a copy in a zip file with the same file naming scheme.
     */
    buildTypes {

        /*
        We define some buildConfigField defs here to use
         */
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def LOG_HTTP_REQUESTS = "LOG_HTTP_REQUESTS"
        def REPORT_CRASHES = "REPORT_CRASHES"
        def ENABLE_VIEW_SERVER = "ENABLE_VIEW_SERVER"
        def ENABLE_SHARING = "ENABLE_SHARING"
        def DEBUG_IMAGES = "DEBUG_IMAGES"


        debug {
            debuggable true
            minifyEnabled false
            // dagger jacaco conflict issue is resolved, dx change after buildtoolsversion 21
            testCoverageEnabled true

            // here we go, versionNameSuffix with build date!
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev-' + rootProject.ext.buildTime

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, TRUE
            buildConfigField BOOLEAN, ENABLE_SHARING, TRUE
            buildConfigField BOOLEAN, DEBUG_IMAGES, TRUE


        }



        release {
            minifyEnabled false

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE



            /*
            Separate out your proguard rules into files for different areas/groups as it makes it easier to
            update the configurations.
             */
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro' , 'proguard-googleplayservices.txt', 'proguard-butterknife.txt','proguard-dagger1.txt','proguard.project.txt'

            applicationIdSuffix '.release'
            versionNameSuffix '-release-' + rootProject.ext.buildTime
            signingConfig signingConfigs.release

        }
    }

    /*
    Lint is already setup as a plugin to iterate over all build variants.
     */
    lintOptions {
        lintConfig file("okio-custom-lint.xml")

        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true

        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("${rootProject.ext.lintReportsDir}/lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("${rootProject.ext.lintReportsDir}/lint-report.html")

        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // Set the severity of the given issues to fatal (which means they will be
        // checked during release builds (even if the lint target is not included)
        fatal 'NewApi', 'InlineApi'
        // Set the severity of the given issues to error
        error 'Wakelock', 'TextViewEdits'
        // Set the severity of the given issues to warning
        warning 'ResourceAsColor'
        // Set the severity of the given issues to ignore (same as disabling the check)
        ignore 'TypographyQuotes'


    }

}



/*
Reports get generated and placed in build/spoon/${Test_VARIANT} folder.
To instead of running on all connected devices to execute on a specific
set of devices uncomment devices line and replace serial number with the
serial numbers of devices you want to target.
 */
spoon {
    // devices = ['333236E9AE5800EC']
    debug = true
    //className = 'fully.qualified.TestCase'

    //methodName = 'testMyApp'
}

/*
Code block set up so that we dynmaically generate some tasks for codeqa and javadocs
to execute on both debug and release trunks and comput apk file ouput for a app module
project that does not have any product flavors or flavor dimensions. Do not use if
you have product flavors and flavor dimensions as it will bomb.
 */
project.afterEvaluate {
    def archiveVariant = ["release", "debug"]
    def codeqaCheckstyleVariant = ["release", "debug"]
    def codeqaClassycleVariant = ["release", "debug"]
    def codeqaJavancssVariant = ["release", "debug"]
    def codeqaJdependVariant = ["release", "debug"]
    def javadocVariant = ["release", "debug"]
    def archiveJavadocVariant = ["release", "debug"]
    def jarJavadocVariant = ["releasse", "debug"]

    android.applicationVariants.all { variant ->

        //rename apk file  block
        variant.outputs[0].outputFile = new File(variant.outputs[0].outputFile.parent,
                project.ext.ourProjectName + "_"
                        + variant.buildType.name + "_"
                        + android.defaultConfig.versionCode + "_"
                        + android.defaultConfig.versionName + ".apk")
        // copy apk block
        if (variant.name in archiveVariant) {
            def taskSuffix = variant.buildType.name.capitalize()
            def version = "${android.defaultConfig.versionCode}(${android.defaultConfig.versionName})"
            def destination = "${rootProject.projectDir}/archive/apks/${project.ext.ourProjectName}/${variant.name}/${version}"
            def assembleTaskName = "assemble${taskSuffix}"
            if (tasks.findByName(assembleTaskName)) {
                def copyAPKTask = tasks.create(name: "archive${taskSuffix}", type: Copy) {
                    description "Archive/copy apk and proguard mappings.txt to a versioned folder."
                    from("${buildDir}") {
                        include "**/proguard/${variant.buildType.name}/mapping.txt"
                        include "**/apk/${variant.outputs[0].outputFile.name}"
                    }
                    into destination
                    eachFile { file ->
                        file.path = file.name // to get flat copy

                    }
                    includeEmptyDirs = false

                }
                tasks[assembleTaskName].finalizedBy = [copyAPKTask]
            }

        }
        // dynamic javadoc task setup, no automatic hooks
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description = 'Javadoc of $variant.name'
            group = 'documentaion'
            // When JavaBase plugin loads it only sets two javadocs, title and destinationDir
            // thus if we need different we need to set it ourselves
            title = "$project.ext.ourProjectName $android.defaultConfig.versionCode $android.defaultConfig.versionName API"
            // I do not think JavaBase plugin sets docsDir as I was getting symbol not resolved
            destinationDir = new File("${project.buildDir}/docs/${variant.name}/javadoc")
            source = variant.javaCompile.source
            def myAndroidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            def myGooglePlayServicesJar = "${android.sdkDirectory}/extras/google/google_play_services/libproject/google-play-services_lib/libs/google-play-services.jar"
            classpath = files(variant.javaCompile.classpath.files, myAndroidJar, myGooglePlayServicesJar)
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            options {
                docTitle = "Generated Javadoc for $project.ext.ourProjectName for $variant.name"
                windowTitle = "javadoc:$variant.name"
                memberLevel = JavadocMemberLevel.PROTECTED
                author = true
                header = project.ext.ourProjectName
                footer = project.ext.javadocFooter
                linksOffline('http://d.android.com/reference', '${android.sdkdirectory}/docs/reference')
                stylesheetFile = new File("${rootProject.ext.javadocsJDK7Stylesheet}")

            }
            doLast {
                copy {

                    from "${project.buildDir}/docs/${variant.name}/javadoc"
                    into "${rootProject.projectDir}/archive/docs/${variant.name}/${android.defau8ltConfig.versionCode}/javadoc"
                }
            }

        }





    }
}

    /*
    The code-quality plugins that come with gradle are already set to
    execute in dependence of the check anchor task and read the sourceSets of
    product with or without variants correctly. Thus, we put them here to
    differentiate from any other no default code-quality checks you may have.
     */
 task ("androidChekstyle",type: Checkstyle) {
     description = 'Runs checkstyle checks on sourcesets of the android project'
     group = 'verfication'
     ignoreFailures = false
     showViolations = true
     configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
     // remember sourceSets defualts to project.sourceSets
     source = 'src'
     include '**/*.java'
     exclude '**/gen/**'
     // gradle plugin defaults to toolVersion = '5.1'

     classpath = files()

     // default checkstyle task only has an xml report
     reports {
         xml.enabled = true
         xml.destination = "${project.buildDir}/reports/checkstyle"
     }

 }
task ("androidPmd",type: Pmd){
    description = 'runs pmd checks on sourcesets of the android project'
    group = 'verification'
    ignoreFailures = true
    ruleSets = ["basic", "braces", "strings"]
    // gradle plugin defaults to toolVersion = '5.1.1'
    consoleOutput = true
    //remember sourceSets defaults to project.sourceSets
    source = 'src'
    include  '**/*.java'
    exclude '**/gen/**'


    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination = "${project.buildDir}/reports/pmd"
        html.destination = "${project.buildDir}/reports/pmd"
    }
}
task ("androidJdepend", type: JDepend){
    description = 'Runs jdepend chekcs on sourcesets of the anroid project'
    group = 'verfication'

    //gradle plugin defaults to toolVersion= '2.9.1'

    // in projects with multiple variants we will have do something different
    // as than we want jdepend only run on the variant classes
    classesDir = file('build/intermediates/classes/debug')

    // no reportsDir in jdepend plugin and no html reports either
    reports{
        xml.enabled = true
        xml.destination = '${project.buildDir}/reports/jdepend'
    }

}







configurations{
    checkstyle
    pmd
    jdepend
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile rootProject.ext.androidsupportapptcompatv7

    checkstyle 'com.puppycrawl.tools:checkstyle:6.1'
    pmd 'net.sourceforge.pmd:pmd-java:5.2.1'
    jdepend 'jdepend:jdepend:2.9.1'


    compile rootProject.ext.retrofit

    compile rootProject.ext.dagger
    compile rootProject.ext.butterknife

    compile rootProject.ext.otto
    compile rootProject.ext.rxjava
    compile rootProject.ext.timber

    compile rootProject.ext.okhttp
    compile rootProject.ext.okio
    compile rootProject.ext.okhttpurlconnection

    debugCompile rootProject.ext.retrofitmock

    provided rootProject.ext.daggercompiler

    androidTestCompile(rootProject.ext.doubleespresso) {
        exclude group: 'com.squareup.dagger', module: 'dagger'
        exclude group: 'com.android.support', module: 'support-v4'

    }
    androidTestCompile rootProject.ext.doubleespressoandroidsupportv4

    androidTestCompile rootProject.ext.assertjandroid
    androidTestCompile rootProject.ext.assertjandroidsupportv4



    androidTestCompile rootProject.ext.mockito
    androidTestCompile rootProject.ext.dexmaker
    androidTestCompile(rootProject.ext.dexmakermockito) {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
        exclude module: 'mockito-core'

    }

    androidTestCompile rootProject.ext.spoonclient

}